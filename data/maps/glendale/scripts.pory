mapscripts glendale_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: glendalescript_ontransition
    }

script controllernoroute{
    lockall
    msgbox(format("It's too dangerous to go any further without borrowing one of DAD's POKéMON."))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, leavetrailone)
    waitmovement(0)
    releaseall
}
    
movement leavetrailone{
    face_down
    walk_down
}

script dadintro{
    lockall
    msgbox(format("???: Psst.. {PLAYER}!"))
    waitmessage
    applymovement(2, dadreveal)
    waitmovement(0)
    msgbox(format("DAD: It's me. Sssh! Be quiet."))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, controllertodad)
    waitmovement(0)
    msgbox(format("There's a rare POKéMON up there. I've seen it before, but it flees before I can attempt to catch it.\pHey, why don't you try to battle it!\pC'mon, it's easy. Just grab one of the POKéMON from my bag there and send it out!"))
    waitmessage
    releaseall
}

movement dadreveal{
    walk_up
    face_right
}

movement controllertodad{
    walk_left * 3
    walk_up
    face_left
}

script dadnocomeback{
    lockall
    msgbox(format("C'mon kid. Don't be scared!"))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, comebacktodad)
    waitmovement(0)
    releaseall
}

movement comebacktodad{
    walk_up
}

script glendalechoosestarter{
    setflag(FLAG_SYS_POKEMON_GET)
    clearflag(FLAG_DAD_NICKNAME)
    setvar(VAR_GLENDALE_STATE, 1)
    setvar(VAR_DAD_NICKNAMETIME, 0)
	special(ChooseStarter)
    lockall
    removeobject(1)
    setflag(FLAG_DAD_STARTERS)
    msgbox(format("DAD: Well, it escaped. But at least we got some interesting data.\pWhat a fascinating creature... \pLet's get inside now, kid."))
    waitmessage
    warp(MAP_DADLAB, -1, 5, 6)
    releaseall
}



script glendalescript_ontransition{
    if(var(VAR_GLENDALE_STATE) == 0) {
        setrespawn(HEAL_LOCATION_CONTROLLERS_HOUSE)
        setflag(FLAG_DAD_NICKNAME)
        setflag(FLAG_DAD_NORMAL)
        setflag(FLAG_DAD_GIVE)
    }
    elif(var(VAR_GLENDALE_STATE) == 1){
        setflag(FLAG_DAD_NICKNAME)
        setflag(FLAG_DAD_GIVE)
        clearflag(FLAG_DAD_NORMAL)
    }
    elif(var(VAR_GLENDALE_STATE) == 2){
        setflag(FLAG_DAD_NICKNAME)
        setflag(FLAG_DAD_NORMAL)
        clearflag(FLAG_DAD_GIVE)
    }
    end
}

script glendalelabsign{
    lockall
    msgbox(format("PROF. STERN's Lab"))
    waitmessage
    releaseall
}

script glendalefatman{
    lockall
    msgbox(format("I've heard there's a META-ZONE where you can play the SAFARI GAME in a virtual world!\pThe power of science is amazing."), MSGBOX_NPC)
    waitmessage
    releaseall
}

script glendaleboy{
    lockall
    msgbox(format("Tell your dad he better give me a POKéMON!"), MSGBOX_NPC)
    waitmessage
    releaseall
}


